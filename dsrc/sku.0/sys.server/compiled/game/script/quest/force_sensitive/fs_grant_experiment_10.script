/**
 * Copyright (c) ©2000-2002 Sony Online Entertainment Inc.
 * All Rights Reserved
 *
 * Title:        quest.force_sensitive.fs_grant_experiment_10.script
 * Description:  Attached to the prize a player gets for the fs village phase 3 community crafting quest.
 *               It will give them 10% of the xp they need for force_sensitive_crafting_mastery_experimentation_01
 *
 * @author       $Author: Steve Jakab$
 * @version      $Revision: 0$
 */

/***** INCLUDES ********************************************************/

include library.utils;


/***** CONSTANTS *******************************************************/

const string DATATABLE_SKILLS = "datatables/skill/skills.iff";
const string SKILL = "force_sensitive_crafting_mastery_experimentation_01";


/***** TRIGGERS ********************************************************/

trigger OnAttach()
{
	// we expect the object we are attached to to be in the player's inventory
	obj_id player = utils.getContainingPlayer(self);
	if ( isIdValid(player) )
	{
		int row = dataTableSearchColumnForString(SKILL, "NAME", DATATABLE_SKILLS);
		if ( row >= 0 )
		{
			float xp = dataTableGetInt(DATATABLE_SKILLS, row, "XP_COST");
			string xpType = dataTableGetString(DATATABLE_SKILLS, row, "XP_TYPE");
			if ( xp > 0 && xpType != null )
			{
				grantExperiencePoints(player, xpType, (int)(xp * 0.1f));
			}
			else
			{
				CustomerServiceLog("fs_village", "WARNING: fs_grant_experiment_10 could not find xp data for skill " + SKILL +
					" in the skills datatable");
			}
		}
		else
		{
			CustomerServiceLog("fs_village", "WARNING: fs_grant_experiment_10 could not find skill " + SKILL +
				" in the skills datatable");
		}
	}
	else
	{
		CustomerServiceLog("fs_village", "WARNING: fs_grant_experiment_10 could not find player containing object " + self);
	}

	messageTo(self, "handleDetachMessage", null, 1.0f, true);
	return SCRIPT_CONTINUE;
}

messageHandler handleDetachMessage()
{
	detachScript(self, "quest.force_sensitive.fs_grant_experiment_10");
	return SCRIPT_CONTINUE;
}





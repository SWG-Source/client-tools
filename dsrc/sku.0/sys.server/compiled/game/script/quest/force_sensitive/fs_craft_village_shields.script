/**
 * Copyright (C)2004 Sony Online Entertainment Inc.
 * All Rights Reserved
 *
 * Title:        quest.force_sensitive.fs_craft_village_shields.script
 * Description:  Handles the "craft village shields" quest for the force sensitive village
 * @author       $Author: Steve Jakab $
 * @version      $Revision: #1 $
 */

/***** INCLUDES ********************************************************/

include library.community_crafting;
include library.fs_dyn_village;
include library.fs_quests;
include library.quests;
include library.utils;


/***** CONSTANTS *******************************************************/

const string QUEST_MAIN            = "fs_phase_3_craft_shields_main";
const string QUEST_STEP_01         = "fs_phase_3_craft_shields_01";
const string QUEST_STEP_02         = "fs_phase_3_craft_shields_02";


/***** TRIGGERS ********************************************************/

trigger OnLogin()
{
	utils.removeScriptVar(self, fs_dyn_village.OBJVAR_MY_MASTER_OBJECT);
	utils.removeScriptVar(self, fs_dyn_village.OBJVAR_CURRENT_PHASE);
	requestVillageMaster(self);
	requestVillagePhase(self);
	return SCRIPT_CONTINUE;
}

/***** FUNCTIONS *******************************************************/

messageHandler handleRetryGetVillageManager()
{
	requestVillageMaster(self);
	return SCRIPT_CONTINUE;
}

messageHandler handleRetryGetVillagePhase()
{
	requestVillagePhase(self);
	return SCRIPT_CONTINUE;
}

messageHandler handleGetVillageManager()
{
	boolean rslt = false;
	string key = "";
	if (params.containsKey("key"))
	{
		key = params.getString("key");
	}

	if (params.containsKey("success"))
	{
		rslt = params.getBoolean("success");
	}

	if (rslt && key != null && key.length() > 0)
	{
		obj_id id = null;
		if (params.containsKey(key))
		{
			id = params.getObjId(key);
			if (isIdValid(id))
			{
				utils.setScriptVar(self, fs_dyn_village.OBJVAR_MY_MASTER_OBJECT, id);
				checkVillagePhase(self);
				return SCRIPT_CONTINUE;
			}
		}
	}
	messageTo(self, "handleRetryGetVillageManager", null, 30, false);
	return SCRIPT_CONTINUE;
}

messageHandler handleGetVillagePhase()
{
	boolean rslt = false;
	string key = "";
	if (params.containsKey("key"))
	{
		key = params.getString("key");
	}

	if (params.containsKey("success"))
	{
		rslt = params.getBoolean("success");
	}

	if (rslt && key != null && key.length() > 0)
	{
		if (params.containsKey(key))
		{
			int phase = params.getInt(key);
			if ( phase >= 1 )
			{
				utils.setScriptVar(self, fs_dyn_village.OBJVAR_CURRENT_PHASE, phase);
				checkVillagePhase(self);
				return SCRIPT_CONTINUE;
			}
		}
	}
	messageTo(self, "handleRetryGetVillagePhase", null, 30, false);
	return SCRIPT_CONTINUE;
}

void requestVillageMaster(obj_id self)
{
	if ( !utils.hasScriptVar(self, fs_dyn_village.OBJVAR_MY_MASTER_OBJECT) )
	{
		messageTo(self, "handleRetryGetVillageManager", null, 60.0f, false);
		fs_dyn_village.getRegisteredObjIdFromClusterWideData(
			fs_dyn_village.CLUSTER_OBJID_KEY_MASTER, "handleGetVillageManager", self);
	}
	else
		checkVillagePhase(self);
}

void requestVillagePhase(obj_id self)
{
	if ( !utils.hasScriptVar(self, fs_dyn_village.OBJVAR_CURRENT_PHASE) )
	{
		messageTo(self, "handleRetryGetVillagePhase", null, 60.0f, false);
		fs_dyn_village.getRegisteredIntegerFromClusterWideData(
			fs_dyn_village.CLUSTER_INT_KEY_CUR_PHASE, "handleGetVillagePhase", self);
	}
	else
		checkVillagePhase(self);
}

void checkVillagePhase(obj_id self)
{
	if (!utils.hasScriptVar(self, fs_dyn_village.OBJVAR_MY_MASTER_OBJECT) ||
		!utils.hasScriptVar(self, fs_dyn_village.OBJVAR_CURRENT_PHASE))
	{
		return;
	}

	int currentPhase = utils.getIntScriptVar(self, fs_dyn_village.OBJVAR_CURRENT_PHASE);
	if ( currentPhase != 3 )
	{
		testAbortQuest(self);
	}
	else
	{
		int questIdMain = quests.getQuestId(QUEST_MAIN);
		int questId01 = quests.getQuestId(QUEST_STEP_01);
		int questId02 = quests.getQuestId(QUEST_STEP_02);
		if ( questIdMain > -1 && questId01 > -1 && questId02 > -1 )
		{
			// handle switching from 1-phase quest to 2-phase quest, and the bug that resulted from it
			if ( !isQuestComplete(self, questIdMain) )
			{
				boolean finishQuest = false;
				if ( isQuestActive(self, questId02) )
				{
					quests.complete(QUEST_STEP_02, self, false);
					clearCompletedQuest(self, questId02);
				}
				else if ( isQuestComplete(self, questId02) )
				{
					finishQuest = true;
					clearCompletedQuest(self, questId02);
				}
				// set myself up as if I had completed the main task
				quests.activate(QUEST_MAIN, self, null);
				if ( hasObjVar(self, "_notskill.schematics.1700583901") )
				{
					// we've already gotten the schematics
					quests.complete(QUEST_STEP_01, self, true);
					if ( finishQuest )
					{
						messageTo(self, "handleCommunityCraftingMinimumQuantityMet", null, 0.1f, true);
					}
				}
			}
		}
	}
}

messageHandler msgQuestAbortPhaseChange()
{
	testAbortQuest(self);
	return SCRIPT_CONTINUE;
}

void testAbortQuest(obj_id self)
{
	int questIdMain = quests.getQuestId(QUEST_MAIN);
	int questId01 = quests.getQuestId(QUEST_STEP_01);
	int questId02 = quests.getQuestId(QUEST_STEP_02);
	if ( questIdMain > -1 && questId01 > -1 && questId02 > -1 )
	{
		if (!isQuestComplete(self, questId02))
		{
			// the player didn't finish the quest, let them try again
			if (isQuestActive(self, questId02))
				quests.complete(QUEST_STEP_02, self, false);
			if (isQuestActive(self, questId01))
				quests.complete(QUEST_STEP_01, self, false);
			if (isQuestActive(self, questIdMain))
				quests.complete(QUEST_MAIN, self, false);
			if (isQuestComplete(self, questId02))
				clearCompletedQuest(self, questId02);
			if (isQuestComplete(self, questId01))
				clearCompletedQuest(self, questId01);
			if (isQuestComplete(self, questIdMain))
				clearCompletedQuest(self, questIdMain);
		}
	}
	detachScript(self, "quest.force_sensitive.fs_craft_village_shields");
}

/**
 * Verify our quest status for the off planet bug.
 */
messageHandler handleVerifyQuestStatus()
{
	// get the master object and make sure it's on our server
	obj_id masterObject = null;
	if (utils.hasScriptVar(self, fs_dyn_village.OBJVAR_MY_MASTER_OBJECT))
		masterObject = utils.getObjIdScriptVar(self, fs_dyn_village.OBJVAR_MY_MASTER_OBJECT);
	if ( !exists(masterObject) )
	{
		if ( !isIdValid(masterObject) )
			requestVillageMaster(self);
		messageTo(self, "handleVerifyQuestStatus", null, 15.0f, false);
		return SCRIPT_CONTINUE;
	}

	int questIdMain = quests.getQuestId(QUEST_MAIN);
	int questId01 = quests.getQuestId(QUEST_STEP_01);
	int questId02 = quests.getQuestId(QUEST_STEP_02);
	if ( questIdMain > -1 && questId01 > -1 && questId02 > -1 )
	{
		if (!isQuestComplete(self, questId02))
		{
			quests.activate(QUEST_MAIN, self, null);
			quests.complete(QUEST_MAIN, self, true);
			if ( hasObjVar(self, "community_crafting.schematics") )
			{
				quests.complete(QUEST_STEP_01, self, true);
				int itemsNeeded = community_crafting.getNumIngredientsNeededByPlayer(masterObject, self);
				if ( itemsNeeded == 0 )
				{
					messageTo(self, community_crafting.MSG_HANDLER_MIN_QUANTITY_MET, null, 5.0f, true);
				}
			}
		}
	}
	return SCRIPT_CONTINUE;
}



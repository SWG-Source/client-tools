/**
 * Copyright (c) ©2000-2002 Sony Online Entertainment Inc.
 * All Rights Reserved
 *
 * Title:        trap_webber.script
 * Description:  webber
 * @author       $Author:$
 * @version      $Revision: #1 $
 */

//------------------------------------------------
// Includes
//------------------------------------------------

include ai.ai_combat;
include library.combat;
include library.prose;
include library.ai_lib;
include library.utils;

//------------------------------------------------
// Inherits
//------------------------------------------------

inherits item.trap.trap_base;

//------------------------------------------------
// Constants
//------------------------------------------------

const int 			TRAP_DIFF 						= 5;

const string_id		SID_SYS_EFFECT					= new string_id( "trap/trap", "trap_melee_def_1_effect" );
const string_id		SID_NO_EFFECT					= new string_id( "trap/trap", "trap_effect_no" );

//------------------------------------------------
// Methods
//------------------------------------------------

//------------------------------------------------
// OnAttach
//------------------------------------------------

trigger OnAttach()
{
	if (!hasObjVar(self, "droid_trap"))
	{
		setObjVar( self, "trapDiff", TRAP_DIFF );
		setObjVar( self, "trapName", "melee_def_1" );
		setCount( self, 5 );
	}

	return SCRIPT_CONTINUE;
}

//------------------------------------------------
// trapHit
//------------------------------------------------

messageHandler trapHit()
{
	if ( params == null )
		return SCRIPT_CONTINUE;

	obj_id target = params.getObjId( "target" );
	if ( (target == null) || (target == obj_id.NULL_ID) )
		return SCRIPT_CONTINUE;
	if ( !ai_lib.isMonster( target ) || isIncapacitated( target ) || isDead( target ) )
		return SCRIPT_CONTINUE;

	// Get player.
	obj_id player = params.getObjId( "player" );
	
	if ( utils.hasScriptVar( target, "trapmod.melee_defense" ) )
	{
		prose_package pp = prose.getPackage( SID_NO_EFFECT, target );
		sendSystemMessageProse( player, pp );
		return SCRIPT_CONTINUE;		
	}

	// Affect the target.
	dictionary outparams = new dictionary();
	outparams.put( "melee_defense", 30 );
	outparams.put( "prefix", "melee_def_1" );
	messageTo( target, "applyModTrap", outparams, 0.f, false );
	prose_package pp = prose.getPackage( SID_SYS_EFFECT, target );
	sendSystemMessageProse( player, pp );

	// If they don't have an enemy, make them attack us.
	if (!hasObjVar(self, "droid_trap"))
	{
		if ( !ai_lib.isInCombat( target ) )
			startCombat( target, player );
	}
	else
	{
		if ( !ai_lib.isInCombat( target ) )
			startCombat( target, self );
	}

	// Grant XP.
	float chance = params.getFloat( "chance" );
	grantTrapXP( player, target, 1.f );

	return SCRIPT_CONTINUE;
}

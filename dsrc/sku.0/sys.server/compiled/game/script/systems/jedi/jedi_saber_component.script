include library.ai_lib;
include library.jedi;
include library.jedi_trials;
include library.colors_hex;
include library.sui;
include library.utils;
include library.static_item;

const string SHADER_TABLE			= "datatables/appearance/alternate_lightsaber_shaders.iff";

trigger OnInitialize()
{
	//these are all various script fix ups for the various stages Jedi pearls have gone through
	//check for pre clicky combat
	detachScript(self, "systems.crafting.weapon.component.crafting_weapon_component_attribute");
	if(!static_item.isStaticItem(self))
	{
		jedi.clickyCombatPearlFix(self);
	}


	//Yah for more clean up code, have to put this is for post NGE coversion clean up
	string template = getTemplateName(self);
	if (template.endsWith("lance_module_force_crystal.iff"))
	{
		removeObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_MIN_DMG);
		removeObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_MAX_DMG);
	}

	removeObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_SPEED);
	removeObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_WOUND);
	removeObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_ATTACK_COST);
	removeObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_FORCE);
	removeObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_ACCURACY);

	return SCRIPT_CONTINUE;
}

trigger OnAttach()
{
	if(!static_item.isStaticItem(self))
	{
		// otherwise Store level of creature we came from
		int level = -1;
		obj_id inv = getContainedBy(self);
		LOG("jedi", "Crystal created in - "+inv);
		if (inv != null)
		{
			if (isIdValid(inv))
			{
				obj_id target = getContainedBy(inv);
				LOG("jedi", "    Inventory contained by - "+target);
				if (isIdValid(target))
				{
					const string mobType = getCreatureName(target);
					LOG("jedi", "    Mob type = "+mobType);

					level = getLevel(target);
					LOG("jedi", "    level = "+level);
				}
			}
			if (level == -1)
				level = rand (1, 50);

			jedi.initializeCrystal(self, level);
		}
		else
		{
			dictionary data = new dictionary();
			data.put("attempts", 0);
			messageTo(self, "setCrystalLevel", data, 0.5f, false);
		}
	}
	return SCRIPT_CONTINUE;
}

messageHandler setCrystalLevel()
{
	int attempts = params.getInt("attempts");

	int level = -1;
	obj_id inv = getContainedBy(self);
	LOG("jedi", "Crystal created in - "+inv);
	if (inv != null)
	{
		if (isIdValid(inv))
		{
			obj_id target = getContainedBy(inv);
			LOG("jedi", "    Inventory contained by - "+target);
			if (isIdValid(target))
			{
				const string mobType = getCreatureName(target);
				LOG("jedi", "    Mob type = "+mobType);

				level = getLevel(target);
				LOG("jedi", "    level = "+level);
			}
		}
		if (level == -1)
			level = rand (1, 50);

		jedi.initializeCrystal(self, level);
	}
	else
	{
		attempts++;

		if (attempts < 5)
		{
			params.put("attempts", attempts);
			messageTo(self, "setCrystalLevel", params, 0.5f, false);
		}
		else
		{
			jedi.initializeCrystal(self, rand (1, 50));
		}
	}

	return SCRIPT_CONTINUE;
}


trigger OnDestroy()
{
	if (isDisabled(self))
		return SCRIPT_CONTINUE;

	csLogCrystal(self, "destroyed");

	obj_id inv = getContainedBy(self);
	obj_id saber = getContainedBy(inv);

	if (!jedi.isLightsaber(saber))
		return SCRIPT_CONTINUE;

	obj_id[] contents = getContents(inv);

	if (contents == null || contents.length == 0)
		return SCRIPT_CONTINUE;

	for (int i = 0; i < contents.length; i++)
	{
		if (contents[i] == self)
		{
			jedi.removeCrystalStats(saber, self);
			break;
		}
	}

	return SCRIPT_CONTINUE;
}

trigger OnObjectDisabled(obj_id killer)
{
	obj_id inv = getContainedBy(self);
	obj_id saber = getContainedBy(inv);

	csLogCrystal(self, "decayed");

	LOG ("saber_test", "Crystal ("+self+") disabled.");

	if (jedi.isLightsaber(saber))
		jedi.removeCrystalStats(saber, self);

	string name = getEncodedName(self);
	if (name.startsWith("@"))
		name = localize(getNameStringId(self));
	name = "\\"+colors_hex.RED+""+name + " (broken)\\#.";

	LOG ("saber_test", "Setting Crystal name to: "+name);

	setName(self, name);

	return SCRIPT_CONTINUE;
}

trigger OnObjectMenuRequest(obj_id player, menu_info mi)
{
	if (!jedi.isForceSensitiveLevelRequired(player, jedi.MIN_CRYSTAL_TUNE_PLAYER_LEVEL))
	{
		return SCRIPT_CONTINUE;
	}

	if(!utils.isNestedWithin(self, player))
	{
		return SCRIPT_CONTINUE;
	}

	if (canManipulate(player, self, false, true, 15, true))
	{
		if (!jedi.isCrystalTuned(self))
		{
			menu_info_data mid = mi.getMenuItemByType(menu_info_types.SERVER_PET_OPEN);
			if ( mid != null )
				mid.setServerNotify(true);
			else
				mi.addRootMenu (menu_info_types.SERVER_PET_OPEN, new string_id("jedi_spam","tune_crystal"));
		}
	}

  	return SCRIPT_CONTINUE;
}

trigger OnObjectMenuSelect(obj_id player, int item)
{
	if (!jedi.isForceSensitiveLevelRequired(player, jedi.MIN_CRYSTAL_TUNE_PLAYER_LEVEL))
	{
		return SCRIPT_CONTINUE;
	}

	if (canManipulate(player, self, false, true, 15, false))
	{
		if (item == menu_info_types.SERVER_PET_OPEN)
		{
			verifyTune(player);
		}
	}

	return SCRIPT_CONTINUE;
}

void verifyTune(obj_id player)
{
	obj_id self = getSelf();

	// Create the dialog page.
	int pid = sui.createSUIPage( sui.SUI_MSGBOX, self, player, "handleVerifyTune" );

	// Add elements text.
	setSUIProperty(pid, "", "Size", "250,175");
	setSUIProperty(pid, sui.MSGBOX_TITLE, sui.PROP_TEXT, utils.packStringId(new string_id("jedi_spam", "confirm_tune_title")));
	setSUIProperty(pid, sui.MSGBOX_PROMPT, sui.PROP_TEXT, utils.packStringId(new string_id("jedi_spam", "confirm_tune_prompt")));

	// Add buttons.
	sui.msgboxButtonSetup(pid, sui.OK_CANCEL);

	// Show dialog.
	sui.showSUIPage( pid );
}

messageHandler handleVerifyTune()
{
	if ( (params == null) || (params.isEmpty()) )
	{
		return SCRIPT_CONTINUE;
	}

	obj_id player = sui.getPlayerId(params);
	int bp = sui.getIntButtonPressed(params);
	switch (bp)
	{
		case sui.BP_CANCEL:
			return SCRIPT_CONTINUE;

		case sui.BP_OK:
			tuneCrystal(player);
			return SCRIPT_CONTINUE;

	}

	return SCRIPT_CONTINUE;
}



void tuneCrystal(obj_id player)
{
	obj_id self = getSelf();
	/*
	const string COMPONENT_DATATABLE = "datatables/jedi/saber_component_ranges.iff";



	if (hasObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_LEVEL))
	{
		int level = getIntObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_LEVEL);
		if (level > -1)
		{
			int quality = jedi.getCrystalQuality(level);

			int forceCost = 100 + (quality * 75);

			if (jedi.drainForcePower(player, forceCost))
			{
				dictionary dctWeaponInfo = dataTableGetRow(COMPONENT_DATATABLE, getTemplateName(self));

				int minDamage  = (int)dctWeaponInfo.getFloat("fltMinDamage");
				int maxDamage  = (int)dctWeaponInfo.getFloat("fltMaxDamage");
				float minSpeed = dctWeaponInfo.getFloat("fltMinSpeed");
				float maxSpeed = dctWeaponInfo.getFloat("fltMaxSpeed");
				float minForce = dctWeaponInfo.getFloat("fltMinForcePower");
				float maxForce = dctWeaponInfo.getFloat("fltMaxForcePower");
				float minWound = dctWeaponInfo.getFloat("fltMinWoundChance");
				float maxWound = dctWeaponInfo.getFloat("fltMaxWoundChance");
				int minAttack  = dctWeaponInfo.getInt("minAttackCost");
				int maxAttack  = dctWeaponInfo.getInt("maxAttackCost");
				int minAccuracy = dctWeaponInfo.getInt("minAccuracy");
				int maxAccuracy = dctWeaponInfo.getInt("maxAccuracy");

				int   damage = distributedRand(minDamage, maxDamage, level);
				float wound  = distributedRand(minWound, maxWound, level);
				float force  = distributedRand(minForce, maxForce, level);
				float speed  = distributedRand(minSpeed, maxSpeed, level);
				int   attack = distributedRand(minAttack, maxAttack, level);
				int   accuracy = distributedRand(minAccuracy, maxAccuracy, level);

				setObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_MIN_DMG, damage);
				setObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_MAX_DMG, damage);
				setObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_WOUND, wound);
				setObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_FORCE, force);
				setObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_SPEED, speed);
				setObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_ATTACK_COST, attack);
				setObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_ACCURACY, accuracy);
			}
			else
				return;
		}
	}
	*/


	//setObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_LEVEL, -1);

	//this is early form of bio link basically
	setObjVar(self, jedi.VAR_CRYSTAL_OWNER_ID, player);
	setObjVar(self, jedi.VAR_CRYSTAL_OWNER_NAME, getName(player));


	//this is terrible hacky way to change the name green, we set the crafted name basically, this can break at any point
	string name = getEncodedName(self);
	if (name.startsWith("@"))
		name = localize(getNameStringId(self));
	name = "\\"+colors_hex.GREEN+""+name + " (tuned)\\#.";
	setName(self, name);

	sendSystemMessage(player, new string_id("jedi_spam", "crystal_tune_success"));
	sendDirtyObjectMenuNotification(self);
}

int distributedRand(int min, int max, int level)
{
	const int levelMin = 60;
	const int levelMax = 280;

	boolean inverted = false;
	int _min = min;
	int _max = max;

	if (min > max)
	{
		inverted = true;
		int temp = min;
		min = max;
		max = temp;

	}

	//if (level < levelMin) level = levelMin;
	//if (level > levelMax) level = levelMax;

	float rank = (float)(level - levelMin) / (float)(levelMax - levelMin);

	float mid = min + ((max - min) * rank);

	if (mid < min)  { max += (mid-min); mid = min; }
	if (mid > max)  { min += (mid-max); mid = max; }

	int minRand = rand(min, (int)(mid+0.5f));
	int maxRand = rand((int)(mid+0.5f), max);

	int randNum = rand(minRand, maxRand);

	if (inverted)
		randNum = _min + (_max - randNum);

	return randNum;
}

float distributedRand(float min, float max, int level)
{
	const int levelMin = 60;
	const int levelMax = 280;

	boolean inverted = false;
	float _min = min;
	float _max = max;

	if (min > max)
	{
		inverted = true;
		min = _max;
		max = _min;
	}

	//if (level < levelMin) level = levelMin;
	//if (level > levelMax) level = levelMax;

	float rank = (float)(level - levelMin) / (float)(levelMax - levelMin);

	float mid = min + ((max - min) * rank);

	if (mid < min)  { max += (mid-min); mid = min; }
	if (mid > max)  { min += (mid-max); mid = max; }

	float minRand = rand(min, mid);
	float maxRand = rand(mid, max);

	float randNum = rand(minRand, maxRand);

	if (inverted)
		randNum = _min + (_max - randNum);

	return randNum;
}

void csLogCrystal(obj_id crystal, string state)
{
	int damage = getIntObjVar(crystal, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_MIN_DMG);
	float wound = getFloatObjVar(crystal, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_WOUND);
	float force = getFloatObjVar(crystal, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_FORCE);
	float speed = getFloatObjVar(crystal, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_SPEED);

	obj_id player = getContainedBy(crystal);

	while (!isPlayer(player))
	{
		obj_id container = getContainedBy(player);

		if (player != container)
			player = container;
		else
			break;
	}

	CustomerServiceLog("jedi_saber", "A crystal("+crystal+" - "+getTemplateName(crystal)+") owned by %TU was "+state+". (Dam:"+damage+" Spd:"+speed+" Wnd:"+wound+" Frc:"+force+")", player);
}

trigger OnGetAttributes(obj_id player, string[] names, string[] attribs)
{
	int idx = utils.getValidAttributeIndex(names);
	if (idx == -1)
		return SCRIPT_CONTINUE;

	if (jedi.isCrystalTuned(self))
	{
		if (hasObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_MIN_DMG))
		{
			names[idx] = "wpn_comp_damage_min";
			int attrib = getIntObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_MIN_DMG);
			attribs[idx] = Integer.toString(attrib);
			idx++;
			if (idx >= names.length)
				return SCRIPT_CONTINUE;
		}
		if (hasObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_MAX_DMG))
		{
			names[idx] = "wpn_comp_damage_max";
			int attrib = getIntObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_MAX_DMG);
			attribs[idx] = Integer.toString(attrib);
			idx++;
			if (idx >= names.length)
				return SCRIPT_CONTINUE;
		}

		names[idx] = "crystal_owner";
		attribs[idx] = getStringObjVar(self, jedi.VAR_CRYSTAL_OWNER_NAME);
		idx++;
		if (idx >= names.length)
			return SCRIPT_CONTINUE;
	}
	else
	{
		names[idx] = "crystal_owner";
		attribs[idx] = "\\#pcontrast2 "+localize(new string_id("jedi_spam", "crystal_untuned"))+"\\#.";
		idx++;
		if (idx >= names.length)
			return SCRIPT_CONTINUE;

		if (hasObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_QUALITY))
		{
			int qualityLevel = getIntObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_QUALITY);
			names[idx] = "crystal_quality";
			int attrib = jedi.getStaticCrystalQuality(qualityLevel);
			attribs[idx] = utils.packStringId(new string_id("jedi_spam", "crystal_quality_"+attrib));
			idx++;
			if (idx >= names.length)
				return SCRIPT_CONTINUE;
		}
	}

   	if (hasObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_COLOR))
   	{
		names[idx] = "color";
		int attrib = getIntObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_COLOR);
		string strAsciiId = "saber_color_"+attrib;
		//Check to change the shader color
		if(getIntObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_SHADER) > 0)
		{
			int shaderVal = getIntObjVar(self, jedi.VAR_CRYSTAL_STATS + "." + jedi.VAR_SHADER);
			//ping the table for the shader name. Row is shader value minus one.
			strAsciiId = dataTableGetString(SHADER_TABLE, (shaderVal - 1), "colorModification");
		}

		string_id strSpam = new string_id("jedi_spam", strAsciiId);
		string strPackedId = utils.packStringId(strSpam);
		attribs[idx] = strPackedId;
		idx++;
		if (idx >= names.length)
			return SCRIPT_CONTINUE;
   	}

	return SCRIPT_CONTINUE;
}

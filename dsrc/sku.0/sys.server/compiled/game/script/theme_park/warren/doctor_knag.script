include library.ai_lib;
include ai.ai_combat;
include library.factions;
include library.utils;
include library.attrib;
include library.chat;

const string CONVO_FILE			= "theme_park/warren/warren";

const string ALERT_VOLUME_NAME = "alertTriggerVolume";

const string ACTION_ALERT = "alert";
const string ACTION_THREATEN = "threaten";

trigger OnAttach()
{
	if ( getConfigSetting("GameServer", "disableAITriggerVolumes") == null )
		createTriggerVolume( ALERT_VOLUME_NAME, 15.0f, true );

	if ( !hasObjVar( self, "ai.grenadeType" ))
	{
		setObjVar( self, "ai.grenadeType", "object/weapon/ranged/grenade/grenade_fragmentation" );
	}

	setName( self, "" );
	setName( self, new string_id ( "theme_park/warren/warren_system_messages", "name_knag" ));
	return SCRIPT_CONTINUE;
}

trigger OnTriggerVolumeEntered( string volumeName, obj_id breacher )
{
	if ( hasObjVar( breacher, "gm") )
		return SCRIPT_CONTINUE;

	if ( ai_lib.isInCombat( self ) || ai_lib.isInCombat( breacher))
		return SCRIPT_CONTINUE;

	if ( breacher == self )
		return SCRIPT_CONTINUE;

	if ( isIncapacitated(self) || ai_lib.isAiDead( breacher ))
		return SCRIPT_CONTINUE;

	if ( !isPlayer( breacher) )
		return SCRIPT_CONTINUE;

	if ( volumeName == ALERT_VOLUME_NAME && !ai_lib.isInCombat( self ))
	{
		if ( rand(1,5)==1 )
		{
			chat.chat( self, new string_id( CONVO_FILE, "knag_greeting" ));//ramble ramble
		}
		startCombat( self, breacher );
		return SCRIPT_CONTINUE;
	}
	return SCRIPT_CONTINUE;
}

trigger OnIncapacitateTarget(obj_id target)
{
	if ( rand(1,10)==1 )
		chat.chat( self, new string_id( CONVO_FILE, "knag_wins" ));//ramble ramble
	return SCRIPT_CONTINUE;
}

trigger OnEnteredCombat()
{
	if ( rand(1,10)==1 )
		chat.chat( self, new string_id( CONVO_FILE, "knag_attacks" ));//ramble ramble
	return SCRIPT_CONTINUE;
}

trigger OnIncapacitated( obj_id killer )
{
	obj_id warren = getObjIdObjVar( self, "warren.bldg" );
	messageTo( warren, "handleKnagDied", null, 1800, false );
	return SCRIPT_CONTINUE;
}


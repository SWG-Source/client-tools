include library.ai_lib;
include library.combat;
include library.factions;
include library.trial;

trigger OnAttach()
{
	setCreatureCoverVisibility(self, false);
	setInvulnerable(self, true);
	factions.setIgnorePlayer(self);
	detachScript(self, "ai.creature_combat");
	messageTo(self, "hate_link", null, 1.0f, false);
	return SCRIPT_CONTINUE;
}

messageHandler start_observation()
{
	trial.bumpSession(self, "reset");
	
	messageTo(self, "observe_loop", trial.getSessionDict(self, "reset"), 1.0f, false);
	return SCRIPT_CONTINUE;
}

messageHandler observe_loop()
{
	if (!trial.verifySession(self, params, "reset"))
		return SCRIPT_CONTINUE;
		
	obj_id[] players = trial.getValidTargetsInCell(trial.getTop(self), "r7");
	
	if (players == null || players.length == 0)
	{
		dictionary dict = trial.getSessionDict(trial.getTop(self));
		dict.put("triggerType", "triggerId");
		dict.put("triggerName", "exar_reset");
		messageTo(trial.getTop(self), "triggerFired", dict, 0.0f, false);
		return SCRIPT_CONTINUE;
	}
	
	for (int i=0;i<players.length; i++)
	{
		startCombat(self, players[i]);
		addHate(self, players[i], 1.0f);
	}
	
	messageTo(self, "observe_loop", trial.getSessionDict(self, "reset"), 8.0f, false);
	return SCRIPT_CONTINUE;
	
}

messageHandler hate_link()
{
	obj_id[] allobj = trial.getObjectsInCellWithObjVar(trial.getTop(self), "r7", "spawn_id");
	
	resizeable obj_id[] the_team = new obj_id[0];
	
	
	for (int i=0;i<allobj.length;i++)
	{
		string spawn_id = getStringObjVar(allobj[i], "spawn_id");
		
		if (spawn_id.equals("harmony") || spawn_id.equals("chaos") || spawn_id.equals("veng") || spawn_id.equals("wrath"))
		{
			the_team.add(allobj[i]);
		}
	}
	
	the_team.add(self);
	
	allobj = the_team;
	
	ai_lib.establishAgroLink(self, allobj);
	
	return SCRIPT_CONTINUE;
}
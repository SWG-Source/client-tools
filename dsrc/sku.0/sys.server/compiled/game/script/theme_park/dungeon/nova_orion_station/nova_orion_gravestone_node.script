include library.create;
include library.groundquests;
include library.utils;

trigger OnAttach()
{
	if ( utils.hasScriptVar(self, "graveEventInProgress") )
		utils.removeScriptVar(self, "graveEventInProgress");
	
	if ( utils.hasScriptVar(self, "gravestoneSpawned") )
		utils.removeScriptVar(self, "gravestoneSpawned");
		
	if ( utils.hasScriptVar(self, "katiaraSpawned") )
		utils.removeScriptVar(self, "katiaraSpawned");

	return SCRIPT_CONTINUE;
}

trigger OnInitialize()
{
	if ( utils.hasScriptVar(self, "graveEventInProgress") )
		utils.removeScriptVar(self, "graveEventInProgress");
	
	if ( utils.hasScriptVar(self, "gravestoneSpawned") )
		utils.removeScriptVar(self, "gravestoneSpawned");
		
	if ( utils.hasScriptVar(self, "katiaraSpawned") )
		utils.removeScriptVar(self, "katiaraSpawned");

	return SCRIPT_CONTINUE;
}

trigger OnObjectMenuRequest (obj_id player, menu_info menuInfo)
{
	if ( isGod(player) )
	{
		int menu = menuInfo.addRootMenu(menu_info_types.SERVER_MENU1, new string_id("ui_radial", "wave_event_reset"));
	}
		
	return SCRIPT_CONTINUE;
}

trigger OnObjectMenuSelect(obj_id player, int item)
{
	if( item == menu_info_types.SERVER_MENU1 )
	{
		if ( isGod(player) )
		{
			sendSystemMessage(player, "Reseting gravestone event...", "");
			dictionary webster = new dictionary();
			webster.put("player", player);
			messageTo(self, "cleanupNoFinaleEvent", webster, 1, false);
			return SCRIPT_CONTINUE;
		}
	}
	
	return SCRIPT_CONTINUE;
}

messageHandler startNoFinaleEvent()
{
	obj_id player = params.getObjId("player");
	if ( isIdValid(player) )
	{
		if ( !utils.hasScriptVar(self, "graveEventInProgress") )
		{
			utils.setScriptVar(self, "graveEventInProgress", player);

			// spawn gravestone
			string gravestoneTemplate = "object/tangible/quest/nova_orion/nova_orion_katiara_gravestone_display.iff";
			location here = getLocation(self);
			here.x = -26.91f;
			here.y = 29.00f;
			here.z = 15.11f;

			obj_id gravestone = createObject(gravestoneTemplate, here);
			setYaw(gravestone, 130.0f);

			utils.setScriptVar(self, "gravestoneSpawned", gravestone);
			utils.setScriptVar(gravestone, "objParent", self);

			// spawn Katiara's ghost
			here.x = -28.5f;
			here.y = 29.00f;
			here.z = 14.2f;

			obj_id katiara = create.staticObject("nova_orion_katiaras_ghost", here);
			setYaw(katiara, 125.0f);

			utils.setScriptVar(self, "katiaraSpawned", katiara);
			utils.setScriptVar(katiara, "objParent", self);
			utils.setScriptVar(katiara, "myPlayer", player);
			
			dictionary webster = new dictionary();
			webster.put("player", player);
			messageTo(self, "safeResetNoFinaleEvent", webster, 300, false);
		}
	}
	return SCRIPT_CONTINUE;
}

messageHandler safeResetNoFinaleEvent()
{
	obj_id cleanupPlayer = params.getObjId("player");
	
	obj_id currentPlayer = null;
	if ( utils.hasScriptVar(self, "graveEventInProgress") )
	{
		currentPlayer = utils.getObjIdScriptVar(self, "graveEventInProgress");
		utils.removeScriptVar(self, "graveEventInProgress");
	}
	
	if ( !isIdValid(cleanupPlayer) || !isIdValid(currentPlayer) || cleanupPlayer == currentPlayer )
	{
		if ( utils.hasScriptVar(self, "gravestoneSpawned") || utils.hasScriptVar(self, "katiaraSpawned") )
		{
			obj_id gravestone = utils.getObjIdScriptVar(self, "gravestoneSpawned");
			obj_id katiara = utils.getObjIdScriptVar(self, "katiaraSpawned");
			utils.removeScriptVar(self, "gravestoneSpawned");
			utils.removeScriptVar(self, "katiaraSpawned");

			if ( isIdValid(gravestone) )
			{
				destroyObject(gravestone);
			}

			if ( isIdValid(katiara) && exists(katiara) )
			{
				destroyObject(katiara);
			}
		}
	}
	
	return SCRIPT_CONTINUE;
}

messageHandler cleanupNoFinaleEvent()
{
	if ( utils.hasScriptVar(self, "graveEventInProgress") )
	{
		utils.removeScriptVar(self, "graveEventInProgress");
	}

	if ( utils.hasScriptVar(self, "gravestoneSpawned") )
	{
		obj_id gravestone = utils.getObjIdScriptVar(self, "gravestoneSpawned");
		utils.removeScriptVar(self, "gravestoneSpawned");

		if ( isIdValid(gravestone) )
		{
			destroyObject(gravestone);
		}
	}

	if ( utils.hasScriptVar(self, "katiaraSpawned") )
	{
		obj_id katiara = utils.getObjIdScriptVar(self, "katiaraSpawned");
		utils.removeScriptVar(self, "katiaraSpawned");

		if ( isIdValid(katiara) && exists(katiara) )
		{
			destroyObject(katiara);
		}
	}
	
	return SCRIPT_CONTINUE;
}

include library.ai_lib;
include library.utils;
include library.trial;
include library.create;


const string SQUAD_MEMBER				= "som_battlefield_elite_guard";
const boolean LOGGING					= false;

trigger OnIncapacitated(obj_id killer)
{
	trial.bumpSession(self);
	obj_id top = utils.getObjIdScriptVar(self, trial.PARENT);
	messageTo(top, "commanderDied", null, 5, false);
	return SCRIPT_CONTINUE;
}

trigger OnAttach()
{
	trial.bumpSession(self);
	findWayPoints(self);
	trial.setInterest(self);
	trial.markAsDroidArmy(self);
	messageTo(self, "pathToNextPoint", null, 2, false);
	messageTo(self, "spawnEliteGuard", null, 2, false);
	messageTo(self, "performRez", trial.getSessionDict(self), trial.BATTLEFIELD_COMM_REZ_DELAY, false);
	setHibernationDelay(self, 7200);
	return SCRIPT_CONTINUE;
}


messageHandler spawnEliteGuard()
{
	obj_id[] squad = new obj_id[6];
	location baseLoc = getLocation(self);
	setYaw(self, -250);
	
	for (int i=0;i<squad.length;i++)
	{
		location spawnLoc = new location(baseLoc.x, baseLoc.y, baseLoc.z, baseLoc.area, baseLoc.cell);
		squad[i] = create.object(SQUAD_MEMBER, spawnLoc);
		
		if (isIdValid(squad[i]))
		{
			attachScript(squad[i], "theme_park.dungeon.mustafar_trials.valley_battleground.elite_guard");
			utils.setScriptVar(squad[i], trial.PARENT, self);
			ai_lib.setPatrolOncePath(squad[i], utils.getLocationArrayScriptVar(self, "patrolPoints"));
			trial.markAsDroidArmy(squad[i]);		
		}
	}
	
	ai_lib.establishAgroLink(self, squad);
	return SCRIPT_CONTINUE;

}

void findWayPoints(obj_id self)
{
	obj_id objects[] = getObjectsInRange(self, 400);
	if (objects == null || objects.length == 0)
	{
		doLogging("findWayPoints", "Contents list was empty, exiting");
		return;
	}
	
	int pathNum = 0;
	
	if (utils.hasScriptVar(self, "path"))
	{		
		pathNum = utils.getIntScriptVar(self, "path");
	}
	else
	{
		string[] paths = dataTableGetStringColumn(trial.VALLEY_DATA, "path");
		pathNum = rand(0, paths.length -1);
	}
	
	string path = dataTableGetString(trial.VALLEY_DATA, pathNum, "path");
	string[] pathList = split(path, ';');
	
	if (pathList == null || pathList.length == 0)
	{
		doLogging("findWayPoints", "Path list was empty, exiting");
		return;
	}
	resizeable location[] waypoints = new location[0];

	for (int i=0;i<pathList.length;i++)
	{
		for (int k=0;k<objects.length;k++)
		{
			if (hasObjVar(objects[k], "wp_name"))
			{
				if (pathList[i].equals(getStringObjVar(objects[k], "wp_name")))
				{
					utils.addElement(waypoints, getLocation(objects[k]));
				}
			}
		}
	}
	
	
	if (waypoints == null || waypoints.length == 0)
	{
		doLogging("findWayPoints", "No waypoints were found, exiting");
		return;
	}
	
	location[] patrolPoints = waypoints;
	if (patrolPoints.length == 0)
	{
		doLogging("findWayPoints", "Patrol Point list was empty, exiting");
		return;
	}
	
	utils.setScriptVar(self, "patrolPoints", patrolPoints);
}

messageHandler pathToNextPoint()
{
	
	location[] patrolPoints = utils.getLocationArrayScriptVar(self, "patrolPoints");
	ai_lib.setPatrolOncePath(self, patrolPoints);
	
	return SCRIPT_CONTINUE;
}

messageHandler performRez()
{
	if (!trial.verifySession(self, params))
		return SCRIPT_CONTINUE;
		
	obj_id[] corpses = trial.getObjectsInRangeWithScriptVar(self, trial.BATTLEFIELD_DROID_CORPSE, 22f);
	
	if (corpses == null || corpses.length == 0)
	{
		messageTo(self, "performRez", trial.getSessionDict(self), trial.BATTLEFIELD_COMM_REZ_DELAY, false);
		return SCRIPT_CONTINUE;
	}	
	
	playClientEffectLoc(self, trial.PRT_DROID_REVIVE, getLocation(self), 4.0f);
	
	for (int i=0;i<corpses.length && i < 3;i++)
	{
		rezCorpse(corpses[i], getLocation(corpses[i]));
	}
	
	messageTo(self, "performRez", trial.getSessionDict(self), trial.BATTLEFIELD_COMM_REZ_DELAY, false);
	return SCRIPT_CONTINUE;
}

void rezCorpse(obj_id corpse, location corpseLoc)
{
	string template = getTemplateName(corpse);
	location[] patrolPoints = utils.getLocationArrayScriptVar(getSelf(), "patrolPoints");
	
	int type = 0;
	
	if (template.indexOf("cww") > -1)
		type = 1;
		
	if (template.indexOf("union") > -1)
		type = 2;
		
	switch (type)
	{
		case 0:
			obj_id hk = create.object("som_battlefield_droid_soldier", corpseLoc);
			playClientEffectLoc(hk, trial.PRT_DROID_REVIVE, corpseLoc, 4.0f);
			attachScript(hk, "theme_park.dungeon.mustafar_trials.valley_battleground.droid_squad_member");
			messageTo(hk, "pathToNextPoint", null, 3, false);
			utils.setScriptVar(hk, "patrolPoints", patrolPoints);
			destroyObject(corpse);
			trial.markAsDroidArmy(hk);
			return;
		case 1:
			obj_id cww = create.object("som_battlefield_ak_3", corpseLoc);
			playClientEffectLoc(cww, trial.PRT_DROID_REVIVE, corpseLoc, 4.0f);
			attachScript(cww, "theme_park.dungeon.mustafar_trials.valley_battleground.assault_killer_bot");
			messageTo(cww, "pathToNextPoint", null, 3, false);
			utils.setScriptVar(cww, "patrolPoints", patrolPoints);
			destroyObject(corpse);
			trial.markAsDroidArmy(cww);			
			return;
		case 2:
			obj_id union = create.object("som_battlefield_gk_5", corpseLoc);
			playClientEffectLoc(union, trial.PRT_DROID_REVIVE, corpseLoc, 4.0f);
			attachScript(union, "theme_park.dungeon.mustafar_trials.valley_battleground.assault_killer_bot");
			messageTo(union, "pathToNextPoint", null, 3, false);
			utils.setScriptVar(union, "patrolPoints", patrolPoints);
			destroyObject(corpse);
			trial.markAsDroidArmy(union);			
			return;
	}
	
}

void doLogging(string section, string message)
{
	if (LOGGING || trial.VALLEY_LOGGING)
		LOG("logging/forward_commander/"+section, message);	
}
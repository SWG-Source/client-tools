include library.trial;
include library.utils;
include library.create;
include library.ai_lib;
include library.buff;
include library.pclib;
include library.badge;
include library.prose;
include library.instance;

const string[] LOCKED_ROOMS			=
						{
							"mainroom27",
							"hall2",
							"hall7",
							"hall13",
							"centralroom28"
						};

const string BESH_ROOM				= "smallroom24";
const string AUREK_ROOM				= "smallroom21";

const boolean LOGGING				= true;

trigger OnAttach()
{
	return SCRIPT_CONTINUE;
}

messageHandler beginSpawn()
{
	setEventLocks(self);
	setEventStates(self);
	messageTo(self, "spawnGuardians", null, 4, false);
	messageTo(self, "spawnDevistator", null, 4, false);
	messageTo(self, "spawnDroidEngineer", null, 4, false);
	messageTo(self, "spawnDoomBringer", null, 4, false);
	return SCRIPT_CONTINUE;
}

messageHandler dungeonCleanup()
{
	return SCRIPT_CONTINUE;
}

messageHandler remoteCommand()
{
	string command = "null";
	command = params.getString("command");
	
	if (command.equals("setLock"))
	{
		setEventLocks(self);
		return SCRIPT_CONTINUE;
	}
	
	if (command.equals("removeLock"))
	{
		removeEventLocks(self);
		return SCRIPT_CONTINUE;
	}
	
	if (command.equals("lockCell"))
	{
		lockCell(self,params);
		return SCRIPT_CONTINUE;
	}
	
	if (command.equals("setDefaultStates"))
	{
		setEventLocks(self);
		return SCRIPT_CONTINUE;
	}
	if (command.equals("removeEventStates"))
	{
		removeEventStates(self);
		return SCRIPT_CONTINUE;
	}
	
	return SCRIPT_CONTINUE;
}

messageHandler spawnGuardians()
{
	string guard1 = "som_working_hk_58_aurek";
	string guard2 = "som_working_hk_58_besh";
	obj_id[] bossWp = trial.getObjectsInDungeonWithObjVar(self, "boss_wp");
	
	if (bossWp == null || bossWp.length == 0)
	{
		doLogging("spawnGuardians", "No bossWp could be found");
		return SCRIPT_CONTINUE;
	}
	
	location loc1 = null;
	location loc2 = null;
	
	for (int i=0;i<bossWp.length;i++)
	{
		if (getStringObjVar(bossWp[i], "boss_wp").equals("aurek"))
			loc1 = getLocation(bossWp[i]);
		
		if (getStringObjVar(bossWp[i], "boss_wp").equals("besh"))
			loc2 = getLocation(bossWp[i]);
	}
	
	if (loc1 == null || loc2 == null)
	{
		doLogging("spawnGuardians", "Failed to find location for guardian("+loc1+"/"+loc2+")");
		return SCRIPT_CONTINUE;
	}
	obj_id aurek = create.object(guard1, loc1);
	setYaw(aurek, 270);
	utils.setScriptVar(aurek, "name", "aurek");
	attachScript(aurek, "theme_park.dungeon.mustafar_trials.working_droid_factory.aurek_besh");
	
	obj_id besh = create.object(guard2, loc2);
	setYaw(besh, 90);
	utils.setScriptVar(besh, "name", "besh");
	attachScript(besh, "theme_park.dungeon.mustafar_trials.working_droid_factory.aurek_besh");

	
	return SCRIPT_CONTINUE;
}

messageHandler spawnDevistator()
{
	string devistatorString = "som_working_devistator";
	obj_id[] bossWp = trial.getObjectsInDungeonWithObjVar(self, "boss_wp");
	
	if (bossWp == null || bossWp.length == 0)
	{
		doLogging("spawnDevistator", "No bossWp could be found");
		return SCRIPT_CONTINUE;
	}
	location devistatorLoc = null;
	location reactiveRepairLoc = null;
	location inhibitorSupplyLoc = null;
	
	for (int i=0;i<bossWp.length;i++)
	{
		if (getStringObjVar(bossWp[i], "boss_wp").equals("devistator"))
			devistatorLoc = getLocation(bossWp[i]);

		if (getStringObjVar(bossWp[i], "boss_wp").equals("reactive_repair_unit"))
			reactiveRepairLoc = getLocation(bossWp[i]);

		if (getStringObjVar(bossWp[i], "boss_wp").equals("inhibitor_supply"))
			inhibitorSupplyLoc = getLocation(bossWp[i]);
	}
	
	if (devistatorLoc == null || reactiveRepairLoc == null || inhibitorSupplyLoc == null)
	{
		doLogging("spawnDevistator", "Failed to find location for devistaor("+devistatorLoc+"/"+reactiveRepairLoc+"/"+inhibitorSupplyLoc+")");
		return SCRIPT_CONTINUE;		
	}
	
	obj_id devistator = create.object(devistatorString, devistatorLoc);
	setYaw(devistator, 180);
	attachScript(devistator, "theme_park.dungeon.mustafar_trials.working_droid_factory.devistator_boss");
	
	obj_id rru = create.object(trial.OBJECT_REACTIVE_REPAIR, reactiveRepairLoc);
	setYaw(rru, 180);
	attachScript(rru, "theme_park.dungeon.mustafar_trials.working_droid_factory.devistator_repair");
	trial.markAsTempObject(rru, true);
	
	obj_id inhibitorSupply = create.object(trial.OBJECT_INHIBITOR, inhibitorSupplyLoc);
	attachScript(inhibitorSupply, "theme_park.dungeon.mustafar_trials.working_droid_factory.devistator_inhibitor");
	trial.markAsTempObject(inhibitorSupply, false);
	
	return SCRIPT_CONTINUE;
}

messageHandler spawnDroidEngineer()
{
	string droidEngineerString = "som_working_master_droid_engineer";
	obj_id[] bossWp = trial.getObjectsInDungeonWithObjVar(self, "boss_wp");

	if (bossWp == null || bossWp.length == 0)
	{
		doLogging("spawnDroidEngieer", "No bossWp could be found");
		return SCRIPT_CONTINUE;
	}
	
	location mdeLoc = null;
	location clonerOneLoc = null;
	location clonerTwoLoc = null;
	location clonerOneExit = null;
	location clonerTwoExit = null;
	
	for (int i=0;i<bossWp.length;i++)
	{
		if (getStringObjVar(bossWp[i], "boss_wp").equals("droid_engineer"))
			mdeLoc = getLocation(bossWp[i]);

		if (getStringObjVar(bossWp[i], "boss_wp").equals("cloner1"))
			clonerOneLoc = getLocation(bossWp[i]);

		if (getStringObjVar(bossWp[i], "boss_wp").equals("cloner2"))
			clonerTwoLoc = getLocation(bossWp[i]);

		if (getStringObjVar(bossWp[i], "boss_wp").equals("cloner_exit_1"))
			clonerOneExit = getLocation(bossWp[i]);

		if (getStringObjVar(bossWp[i], "boss_wp").equals("cloner_exit_2"))
			clonerTwoExit = getLocation(bossWp[i]);

	}
	
	if (mdeLoc == null || clonerOneLoc == null || clonerTwoLoc == null || clonerOneExit == null || clonerTwoExit == null)
	{
		doLogging("spawnDroidEngineer", "Failed to find location for devistaor("+mdeLoc+"/"+clonerOneLoc+"/"+clonerTwoLoc+"/"+clonerOneExit+"/"+clonerTwoExit+")");
		return SCRIPT_CONTINUE;		
	}
	
	obj_id mde = create.object(droidEngineerString, mdeLoc);
	attachScript(mde, "theme_park.dungeon.mustafar_trials.working_droid_factory.master_droid_engineer");
	setYaw(mde, 121);
	
	obj_id clonerOne = create.object(trial.OBJECT_DE_CLONER, clonerOneLoc);
	attachScript(clonerOne, "theme_park.dungeon.mustafar_trials.working_droid_factory.rapid_assembly_unit");
	utils.setScriptVar(clonerOne, trial.WORKING_CLONER_EXIT, clonerOneExit);
	trial.markAsTempObject(clonerOne, false);
	
	obj_id clonerTwo = create.object(trial.OBJECT_DE_CLONER, clonerTwoLoc);
	attachScript(clonerTwo, "theme_park.dungeon.mustafar_trials.working_droid_factory.rapid_assembly_unit");
	utils.setScriptVar(clonerTwo, trial.WORKING_CLONER_EXIT, clonerTwoExit);
	trial.markAsTempObject(clonerTwo, false);
	
	return SCRIPT_CONTINUE;
}


messageHandler spawnDoomBringer()
{
	obj_id[] bossWp = trial.getObjectsInDungeonWithObjVar(self, "boss_wp");

	if (bossWp == null || bossWp.length == 0)
	{
		doLogging("spawnDoomBringer", "No bossWp could be found");
		return SCRIPT_CONTINUE;
	}
	
	location doomBringerLoc = null;
	location destructionPileLoc = null;
	
	for (int i=0;i<bossWp.length;i++)
	{
		if (getStringObjVar(bossWp[i], "boss_wp").equals("doom_bringer"))
			doomBringerLoc = getLocation(bossWp[i]);

		if (getStringObjVar(bossWp[i], "boss_wp").equals("destruction_pile"))
			destructionPileLoc = getLocation(bossWp[i]);
	}
	
	if (doomBringerLoc == null || destructionPileLoc == null)
	{
		doLogging("spawnDoomBringer", "Failed to find location for doom bringer("+doomBringerLoc+"/"+destructionPileLoc+")");
		return SCRIPT_CONTINUE;		
	}
	
	obj_id doomBringer = create.object("som_working_doom_bringer", doomBringerLoc);
	attachScript(doomBringer, "theme_park.dungeon.mustafar_trials.working_droid_factory.doom_bringer");
	

	obj_id destructionPile = create.object(trial.OBJECT_DECTRUCT_PILE, destructionPileLoc);
	attachScript(destructionPile, "theme_park.dungeon.mustafar_trials.working_droid_factory.doom_target");
	trial.markAsTempObject(destructionPile, false);
	
	messageTo(self, "spawnDoomGuards", null, 0, false);
	return SCRIPT_CONTINUE;
}

messageHandler spawnDoomGuards()
{
	obj_id[] bossWp = trial.getObjectsInDungeonWithObjVar(self, "boss_wp");
	
	if (bossWp == null || bossWp.length == 0)
	{
		return SCRIPT_CONTINUE;
	}
	
	int k=0;
	for (int i=0;i<bossWp.length;i++)
	{
		if (getStringObjVar(bossWp[i], "boss_wp").equals("watcher"+k))
		{
			obj_id watcher = create.object("som_working_hand_of_doom", getLocation(bossWp[i]));
			attachScript(watcher, "theme_park.dungeon.mustafar_trials.working_droid_factory.doom_hand");
			k++;
		}
	}
	return SCRIPT_CONTINUE;
}	

void setEventLocks(obj_id dungeon)
{
	for (int i=0;i<LOCKED_ROOMS.length;i++)
	{
		permissionsMakePrivate(getCellId(dungeon, LOCKED_ROOMS[i]));
	}
}

void removeEventLocks(obj_id dungeon)
{
	for (int i=0;i<LOCKED_ROOMS.length;i++)
	{
		permissionsMakePublic(getCellId(dungeon, LOCKED_ROOMS[i]));
	}
}

void setEventStates(obj_id dungeon)
{
	trial.setAurekKilled(dungeon, false);
	trial.clearAurekBeshKillTime(dungeon);
	trial.setBeshKilled(dungeon, false);
	trial.setDevistatorKilled(dungeon, false);
	trial.setMdeKilled(dungeon, false);
	trial.setMdeEngaged(dungeon, false);
	trial.setPowerCoreDeactivated(dungeon, false);
	trial.setDoomBringerKilled(dungeon, false);
	trial.setRruDeactivated(dungeon, false);
	trial.setDevistatorEngaged(dungeon, false);
	trial.setHkSpawned(dungeon, false);
	trial.setHkDetonated(dungeon, false);
}

messageHandler validateAurekBeshKill()
{
	if (!trial.verifySession(self, params, "validate_ab_kill"))
	{
		return SCRIPT_CONTINUE;
	}
	
	boolean aurekKilled = trial.isAurekKilled(self);
	boolean beshKilled = trial.isBeshKilled(self);
	if (!aurekKilled && !beshKilled)
	{
		doLogging("validateAurekBeshKill", "Neither aurek nor besh are dead, this is wrong");
		return SCRIPT_CONTINUE;
	}
	
	int killTime = trial.getAurekBeshKillTime(self);
	
	if (killTime == 0)
	{
		trial.setAurekBeshKillTime(self);
		messageTo(self, "validateAurekBeshKill", trial.getSessionDict(self, "validate_ab_kill"), trial.WORKING_AUREKBESH_RESPAWN_DELAY, false);
		return SCRIPT_CONTINUE;
	}
	else
	{
		int difference = getGameTime() - killTime;
		if (difference >= trial.WORKING_AUREKBESH_RESPAWN_DELAY)
		{
			if (aurekKilled)
			{
				trial.bumpSession(self, "validate_ab_kill");
				respawnAurek(self);
				utils.setScriptVar(self, trial.WORKING_AUREKBESH_KILL_TIME, 0);
				return SCRIPT_CONTINUE;
			}
			else
			{
				trial.bumpSession(self, "validate_ab_kill");
				respawnBesh(self);
				utils.setScriptVar(self, trial.WORKING_AUREKBESH_KILL_TIME, 0);
				return SCRIPT_CONTINUE;
			}
		}
		else
		{
			if (aurekKilled && beshKilled)
				guardiansDefeated(self);
			else
			{
				doLogging("validateAurekBeshKill", "This message came in less than 30 seconds, but both guardians aren't dead... wtf");
				messageTo(self, "validateAurekBeshKill", trial.getSessionDict(self, "validate_ab_kill"), 5, false);
			}
		}
	}
	
	return SCRIPT_CONTINUE;
}

void respawnAurek(obj_id dungeon)
{
	trial.setAurekKilled(dungeon, false);
	obj_id[] bossWp = trial.getObjectsInDungeonWithObjVar(dungeon, "boss_wp");
	
	if (bossWp == null || bossWp.length == 0)
	{
		doLogging("respawnBesh", "No bossWp could be found");
		return;
	}
	
	location loc1 = null;
	
	for (int i=0;i<bossWp.length;i++)
	{
		if (getStringObjVar(bossWp[i], "boss_wp").equals("aurek"))
			loc1 = getLocation(bossWp[i]);
		
	}
	
	if (loc1 == null)
	{
		doLogging("respawnAurek", "Failed to find location for guardian("+loc1+")");
		return;
	}
	
	obj_id aurek = create.object("som_working_hk_58_aurek", loc1);
	setYaw(aurek, 270);
	utils.setScriptVar(aurek, "name", "aurek");
	attachScript(aurek, "theme_park.dungeon.mustafar_trials.working_droid_factory.aurek_besh");
	
	utils.sendSystemMessagePob(dungeon, trial.TWINS_RESPAWN);
}

void respawnBesh(obj_id dungeon)
{
	trial.setBeshKilled(dungeon, false);
	obj_id[] bossWp = trial.getObjectsInDungeonWithObjVar(dungeon, "boss_wp");
	
	if (bossWp == null || bossWp.length == 0)
	{
		doLogging("respawnBesh", "No bossWp could be found");
		return;
	}
	
	location loc1 = null;
	
	for (int i=0;i<bossWp.length;i++)
	{
		if (getStringObjVar(bossWp[i], "boss_wp").equals("besh"))
			loc1 = getLocation(bossWp[i]);
		
	}
	
	if (loc1 == null)
	{
		doLogging("respawnBesh", "Failed to find location for guardian("+loc1+")");
		return;
	}
	
	obj_id aurek = create.object("som_working_hk_58_besh", loc1);
	setYaw(aurek, 90);
	utils.setScriptVar(aurek, "name", "besh");
	attachScript(aurek, "theme_park.dungeon.mustafar_trials.working_droid_factory.aurek_besh");
	utils.sendSystemMessagePob(dungeon, trial.TWINS_RESPAWN);
}

void guardiansDefeated(obj_id dungeon)
{
	utils.sendSystemMessagePob(dungeon, trial.TWINS_DEFEATED);
	trial.bumpSession(dungeon, "validate_ab_kill");
	trial.setAurekBeshEngaged(dungeon, false);
}

messageHandler devistatorKilled()
{
	trial.setDevistatorKilled(self, true);
	trial.makeCellPublic(self, trial.WORKING_TWO_THREE_STAIR);
	utils.sendSystemMessagePob(self, trial.WORKING_DEVISTATOR_DEFEATED);
	obj_id[] players = instance.getPlayersInInstanceArea(self);
	return SCRIPT_CONTINUE;
}

messageHandler mdeKilled()
{
	obj_id[] fixers = trial.getObjectsInDungeonWithScript(self, "theme_park.dungeon.mustafar_trials.working_droid_factory.mde_repair_droid");
	for (int i=0;i<fixers.length;i++)
	{
		trial.bumpSession(fixers[i]);
		stop(fixers[i]);
		utils.setScriptVar(fixers[i], "fixerNum", i);
		utils.messageTo(fixers, "formFixerOne", params, 3, false);
	}
	return SCRIPT_CONTINUE;
}

messageHandler fixerOneKilled()
{
	trial.setMdeKilled(self, true);
	trial.makeCellPublic(self, trial.WORKING_DE_HALL);
	obj_id[] players = instance.getPlayersInInstanceArea(self);
	return SCRIPT_CONTINUE;
}

void removeEventStates(obj_id dungeon)
{
}

messageHandler doomBringerKilled()
{
	trial.bumpSession(self, "db_control");
	trial.setDoomBringerKilled(self, true);
	trial.makeCellPublic(self, trial.WORKING_MASTER_CONTROL);
	utils.sendSystemMessagePob(self, trial.WORKING_DOOM_DEFEATED);
	trial.setDungeonCleanOutTimer(self);
	trial.sendCompletionSignal(self, trial.WORKING_WIN_SIGNAL);
	obj_id[] players = instance.getPlayersInInstanceArea(self);
	badge.grantBadge(players, "bdg_must_victory_odf");
	return SCRIPT_CONTINUE;
}

messageHandler doomTargetDestroyed()
{
	utils.sendSystemMessagePob(self, trial.WORKING_DOOM_FAILURE);
	
	obj_id[] players = trial.getPlayersInDungeon(trial.getTop(self));
	if (players == null || players.length == 0)
		return SCRIPT_CONTINUE;
		
/*
	for (int i=0;i<players.length;i++)
	{
		setPosture(players[i], POSTURE_DEAD);
		pclib.coupDeGrace(players[i], players[i], false, false);
	}
*/
	obj_id[] objects = trial.getObjectsInDungeonWithObjVar(self, "boss_wp");
	
	for (int i=0;i<objects.length;i++)
	{
		if (getStringObjVar(objects[i], "boss_wp").equals("doom_bringer"))
			playClientEffectLoc(players[0], trial.PRT_WORKING_HK_BOOM_1, getLocation(objects[i]), 1f);

		if (getStringObjVar(objects[i], "boss_wp").equals("destruction_pile"))
			playClientEffectLoc(players[0], trial.PRT_WORKING_HK_BOOM_1, getLocation(objects[i]), 1f);
		
	}

	messageTo(self, "handlePlayerForceRevive", null, 3, false);
	return SCRIPT_CONTINUE;
}

messageHandler handlePlayerForceRevive()
{
	obj_id[] players = trial.getPlayersInDungeon(trial.getTop(self));
	if (players == null || players.length == 0)
		return SCRIPT_CONTINUE;

/*		
	for (int i=0;i<players.length;i++)
	{
		pclib.playerRevive(players[i]);
	}
*/	
	instance.closeInstance(self);
	return SCRIPT_CONTINUE;

}

void lockCell(obj_id dungeon, dictionary params)
{
	string cell = params.getString("cell");
	trial.makeCellPrivate(dungeon, cell);
}

messageHandler spawnHK()
{
	if (!trial.verifySession(self, params, "spawn_hk"))
		return SCRIPT_CONTINUE;
		
	obj_id[] wp = trial.getObjectsInDungeonWithObjVar(self, "hk_sequence");
	
	if (wp == null || wp.length == 0)
	{
		doLogging("spawnHK", "There are no hk_sequence patrol objects");
		return SCRIPT_CONTINUE;
	}
	
	location startLoc = null;
	location endLoc = null;
	
	for (int i=0;i<wp.length;i++)
	{
		if (getStringObjVar(wp[i], "hk_sequence").equals("hk_spawn"))
			startLoc = getLocation(wp[i]);
			
		if (getStringObjVar(wp[i], "hk_sequence").equals("hk_moveto"))
			endLoc = getLocation(wp[i]);
	}
	
	if (startLoc == null || endLoc == null)
	{
		doLogging("spawnHK", "Could not find startloc or end loc: "+startLoc+"/"+endLoc);
		return SCRIPT_CONTINUE;
	}	
	
	obj_id HK = create.object("som_volcano_final_hk47", startLoc);
	
	if (!isIdValid(HK))
	{
		doLogging("spawnHK", "HK Creation failed");
		return SCRIPT_CONTINUE;
	}
	
	trial.bumpSession(self, "spawn_hk");
	messageTo(trial.getTop(self), "detonateHK", trial.getSessionDict(trial.getTop(self), "spawn_hk"), 20, false);
	trial.setHkSpawned(trial.getTop(self), true);
	setInvulnerable(HK, true);
	ai_lib.aiPathTo(HK, endLoc);
	attachScript(HK, "theme_park.dungeon.mustafar_trials.working_droid_factory.hk_movement");
	messageTo(HK, "setRun", null, 1, false);
	aiSetHomeLocation(HK, endLoc);
	
	return SCRIPT_CONTINUE;
}

messageHandler detonateHK()
{
	if (!trial.verifySession(self, params, "spawn_hk"))
		return SCRIPT_CONTINUE;
		
	obj_id[] wp = trial.getObjectsInDungeonWithObjVar(self, "hk_sequence");
	
	if (wp == null || wp.length == 0)
	{
		doLogging("detonateHk", "There are no hk_sequence patrol objects");
		return SCRIPT_CONTINUE;
	}
	
	location[] fireLoc = new location[3];
	location blastLoc = null;
	
	for (int i=0;i<wp.length;i++)
	{
		if (getStringObjVar(wp[i], "hk_sequence").equals("fire1"))
			fireLoc[0] = getLocation(wp[i]);
			
		if (getStringObjVar(wp[i], "hk_sequence").equals("fire2"))
			fireLoc[1] = getLocation(wp[i]);
			
		if (getStringObjVar(wp[i], "hk_sequence").equals("fire3"))
			fireLoc[2] = getLocation(wp[i]);
			
		if (getStringObjVar(wp[i], "hk_sequence").equals("player_blast_trigger"))
			blastLoc = getLocation(wp[i]);
	}
	
	if (blastLoc == null || fireLoc[0] == null || fireLoc[1] == null || fireLoc[2] == null)
	{
		doLogging("detonateHK", "One of the blast WP was null");
		return SCRIPT_CONTINUE;
	}	
	
	obj_id[] players = trial.getPlayersInDungeon(trial.getTop(self));
	obj_id[] playersInBlast = getPlayerCreaturesInRange(blastLoc, 10);
	if (players == null || players.length == 0)
	{
		doLogging("detonateHk", "There are no players in the dungeon");
		return SCRIPT_CONTINUE;
	}
	playClientEffectLoc(players[0], trial.PRT_WORKING_HK_BOOM_1, fireLoc[1], 1f);

	if (playersInBlast != null && playersInBlast.length > 0)
	{
		for (int q=0;q<playersInBlast.length;q++)
		{
			buff.applyBuff(playersInBlast[q], "stop", 5);
			warpPlayer(playersInBlast[q], blastLoc.area, 48, -24, -1, trial.getTop(self), trial.WORKING_MAIN_HALL, 48, -24, -1, "nullCallBack", false);
		}
	}
	
	trial.bumpSession(self, "spawn_hk");
	trial.setHkDetonated(self, true);
	utils.setScriptVar(self, "wps", fireLoc);
	utils.setScriptVar(self, "viewer", players[0]);
	
	messageTo(self, "doExplosionTwo", null, 1, false);
	messageTo(self, "doExplosionThree", null, 3, false);
	
	return SCRIPT_CONTINUE;
}

messageHandler doExplosionTwo()
{
	location[] wp = utils.getLocationArrayScriptVar(self, "wps");
	obj_id viewer = utils.getObjIdScriptVar(self, "viewer");
	playClientEffectLoc(viewer, trial.PRT_WORKING_HK_BOOM_2, wp[0], 1f);
	return SCRIPT_CONTINUE;
}

messageHandler doExplosionThree()
{
	location[] wp = utils.getLocationArrayScriptVar(self, "wps");
	obj_id viewer = utils.getObjIdScriptVar(self, "viewer");
	playClientEffectLoc(viewer, trial.PRT_WORKING_HK_BOOM_3, wp[2], 1f);
	
	messageTo(self, "doHkTaunt", null, 2, false);

	return SCRIPT_CONTINUE;
}

messageHandler doHkTaunt()
{
	obj_id[] hk = trial.getObjectsInDungeonWithScript(trial.getTop(self), "theme_park.dungeon.mustafar_trials.working_droid_factory.hk_movement");
	obj_id[] doomBringer = trial.getObjectsInDungeonWithScript(trial.getTop(self), "theme_park.dungeon.mustafar_trials.working_droid_factory.doom_bringer");
	
	if (hk == null || hk.length == 0 || doomBringer == null || doomBringer.length == 0)
	{
		doLogging("doHkTaunt", "HK or Doombringer was null");
		return SCRIPT_CONTINUE;
	}
	messageTo(hk[0], "doTaunt", null, 0, false);
	messageTo(doomBringer[0], "beginDestruction", null, 12, false);
	return SCRIPT_CONTINUE;
}

messageHandler startDuality()
{
	//trial.setAurekBeshEngaged(self, true);
	messageTo(self, "performDuality", trial.getSessionDict(self, "duality"), 12.0f, false);
	return SCRIPT_CONTINUE;
}

messageHandler performDuality()
{
	if (!trial.verifySession(self, params, "duality"))
	{
		doLogging("performDuality", "Failed to verify session");
		return SCRIPT_CONTINUE;
	}
	
	obj_id[] twins = trial.getObjectsInDungeonWithScriptVar(self, "name");
	obj_id aurek = null;
	obj_id besh = null;
	
	for (int i=0;i<twins.length;i++)
	{
		if (utils.getStringScriptVar(twins[i], "name").equals("aurek"))
			aurek = twins[i];

		if (utils.getStringScriptVar(twins[i], "name").equals("besh"))
			besh = twins[i];
	}
	
	if (!isIdValid(aurek) || !isIdValid(besh) || ai_lib.isDead(aurek) || ai_lib.isDead(besh))
	{
		//trial.setAurekBeshEngaged(self, false);
		doLogging("performDuality", "Invalid ObjId for aurek or best or one is dead");
		trial.bumpSession(self, "duality");
		return SCRIPT_CONTINUE;
	}
	
	if (!ai_lib.isInCombat(aurek) && !ai_lib.isInCombat(besh))
	{
		doLogging("performDuality", "Ending duality for lack of combat");
		trial.bumpSession(self, "duality");
		return SCRIPT_CONTINUE;
	}
	
	obj_id[] aurekPlayers = trial.getValidTargetsInCell(self, AUREK_ROOM);
	obj_id[] beshPlayers = trial.getValidTargetsInCell(self, BESH_ROOM);
	obj_id[] centralPlayers = trial.getValidTargetsInCell(self, "mainroom27");
	
	if (aurekPlayers == null || aurekPlayers.length == 0 || beshPlayers == null || beshPlayers.length == 0)
	{
		//utils.messageTo(aurekPlayers, "dualityDetonation", null, 0.0f, false);
		//utils.messageTo(beshPlayers, "dualityDetonation", null, 0.0f, false);
		utils.messageTo(centralPlayers, "dualityDetonation", null, 0.0f, false);
		messageTo(self, "performDuality", trial.getSessionDict(self, "duality"), 12.0f, false);
		return SCRIPT_CONTINUE;
	}
	
	if (centralPlayers != null && centralPlayers.length > 0)
		utils.messageTo(centralPlayers, "dualityDetonation", null, 0.0f, false);
	
	buff.applyBuff(aurekPlayers, "aurekDuality");
	buff.applyBuff(aurek, "aurekDuality", 21.0f);
	buff.applyBuff(beshPlayers, "beshDuality");
	buff.applyBuff(besh, "beshDuality", 21.0f);
	
	messageTo(self, "performDuality", trial.getSessionDict(self, "duality"), 45.0f, false);
	return SCRIPT_CONTINUE;
}


void doLogging(string section, string message)
{
	if (LOGGING)
		LOG("doLogging/working_controller/"+section, message);
}

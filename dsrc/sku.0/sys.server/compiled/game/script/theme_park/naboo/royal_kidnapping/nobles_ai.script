include library.utils;
include library.ai_lib;
include ai.ai_combat;
include library.chat;
include library.xp;

const string 	CONVO_FILE			= "theme_park/royal_kidnapping/nobles";
const string 	ALERT_VOLUME_NAME 	= "alertTriggerVolume";


const string 	XP_REWARD_TYPE		= "combat_general";
const int		XP_REWARD_AMOUNT	= 250;

trigger OnAttach()
{
	setCondition( self, CONDITION_CONVERSABLE );
	return SCRIPT_CONTINUE;
}

trigger OnObjectMenuRequest(obj_id player, menu_info mi)
{
	if (!utils.hasScriptVar( self, "awardGranted") )
	{
		int mnu = mi.addRootMenu (menu_info_types.CONVERSE_START, null);
		menu_info_data mdata = mi.getMenuItemById(mnu);
		mdata.setServerNotify(false);
	}
	return SCRIPT_CONTINUE;
}

trigger OnStartNpcConversation (obj_id speaker)
{
	if ( ai_lib.isInCombat( self ) || ai_lib.isInCombat( speaker ) || ai_lib.aiIsDead( self ) )
		return SCRIPT_CONTINUE;

	if (utils.hasScriptVar( self, "awardGranted") )
		return SCRIPT_CONTINUE;

	chat.setChatMood( self, chat.MOOD_SCARED );
	stop(self);
	faceToBehavior( self, speaker );
	messageTo( self, "handleAngryAnim", null, 2, false );
	string_id greeting = new string_id (CONVO_FILE, "start");//Eeek a pirate
	string_id response[] = new string_id [1];
	response[0] = new string_id (CONVO_FILE, "reply_1");//I'm not a pirate I'm here to rescue you
	npcStartConversation (speaker, self, CONVO_FILE, greeting, response);
	return SCRIPT_CONTINUE;
}

messageHandler handleAngryAnim()
{
	doAnimationAction( self, "angry" );
	return SCRIPT_CONTINUE;
}

trigger OnNpcConversationResponse(string convo, obj_id player, string_id response)
{
	if ( !convo.equals( CONVO_FILE ) )
		return SCRIPT_CONTINUE;

	npcRemoveConversationResponse(player, response );

	if ( response.getAsciiId().equals("reply_1"))//I'm not a pirate
	{
		doAnimationAction( self, "nod_head_multiple" );
		chat.setChatMood( self, chat.MOOD_HAPPY );
		string_id message = new string_id (CONVO_FILE, "noble_1");//Oh right
		npcSpeak( player, message );
		npcAddConversationResponse( player, new string_id( CONVO_FILE, "noble_1_reply_1" ));//You're free to go.
		return SCRIPT_CONTINUE;
	}
	if ( response.getAsciiId().equals("noble_1_reply_1"))//You're free to go
	{
		doAnimationAction( self, "celebrate" );
		string_id message = new string_id (CONVO_FILE, "noble_2");//Hooray!  You're my hero.
		npcSpeak( player, message );
		utils.setScriptVar( self, "awardGranted", true );
		clearCondition( self, CONDITION_CONVERSABLE );
		xp.grantCombatStyleXp(player, XP_REWARD_TYPE, XP_REWARD_AMOUNT);
		npcEndConversation( player );
		messageTo( self, "handleRunAway", null, 6, false );
		return SCRIPT_CONTINUE;
	}

	return SCRIPT_CONTINUE;
}

messageHandler handleRunAway()
{
	location destLoc = getBuildingEjectLocation(getTopMostContainer(self));
	if ( destLoc==null )
	{
		destLoc = getLocation( getTopMostContainer(self) );
		destLoc.x += 60.0f;
		destLoc.z += 60.0f;
	}
	removeTriggerVolume( ALERT_VOLUME_NAME );
	setWantSawAttackTriggers( self, false );
	setMovementRun( self );
	pathTo( self, destLoc );
	messageTo( self, "handleDelayedDestruct", null, 30, false );
	return SCRIPT_CONTINUE;
}

messageHandler handleDelayedDestruct()
{
	destroyObject( self );
	return SCRIPT_CONTINUE;
}

trigger OnMovePathComplete()
{
	if (utils.hasScriptVar( self, "awardGranted") )
		destroyObject( self );

	return SCRIPT_CONTINUE;
}
